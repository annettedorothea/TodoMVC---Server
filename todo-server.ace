HttpServer Java Dropwizard JDBI3 Liquibase

com.anfelisa.todo


ACE 
	GetAllTodos<TodoList> 
		GET "/todos/all"
		response todoList
	
	GetTodo<Todo> 
		GET "todo/{id}"
			pathParams id
		response 
			id
			description
			createdDateTime
			done
			updatedDateTime
	
	CreateTodo<Todo>
		POST "todos/create"
		payload description
		response 
			id
			createdDateTime
			description
		on success (Todo.create) 
	
	ToggleTodo<TodoToggle> 
		PUT "todos/toggle" 
		queryParams id
		on success ( Todo.toggle )
	
	ToggleAll<ToggleAll>
		PUT "todos/toggle-all" 
		on success ( Todo.toggleAll )
	
	UpdateTodo<Todo>
		PUT "todos/update" 
		payload id description  
		on success (Todo.update)
	
	DeleteTodo<TodoId> 
		DELETE "todos/delete" 
		queryParams id 
		on success (Todo.delete)
	
	ClearDone<ClearDone> 
		DELETE "todos/clear-done" 
		on success (Todo.clearDone) 
		
views 
	Todo {
		create(Todo)
		toggle(TodoToggle)
		toggleAll(ToggleAll)
		delete(TodoId)
		update(Todo)
		clearDone(ClearDone)
	}

models 
	persistent Todo {
		Unique PrimaryKey NotNull String id
		NotNull String description
		NotNull Boolean done
		DateTime createdDateTime
		DateTime updatedDateTime
	}
	
	TodoList {
		List Todo todoList
	}
	
	TodoId {
		NotNull String id
	}

	TodoToggle {
		NotNull String id
		NotNull Boolean done
		Todo todoToBeToggled
		DateTime updatedDateTime
	}

	ToggleAll {
		NotNull Boolean done
		List Todo todosToBeToggled
		DateTime updatedDateTime
	}
	
	ClearDone
	
	
