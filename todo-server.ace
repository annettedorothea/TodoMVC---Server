HttpServer

com.anfelisa.todo
ACE 
	GetAllTodos<TodoList> 
		GET "/todos/all"
		response todoList
	
	CreateTodo<Todo>
		POST "/todos/create"
		payload description
		on success (Todo.create) 
	
	ToggleTodo<TodoToggle> 
		PUT "/todos/toggle" 
		queryParams id
		on success ( Todo.toggle TodoDoneHistory.toggle )
	
	ToggleAll<ToggleAll>
		PUT "/todos/toggle-all" 
		on success ( Todo.toggleAll TodoDoneHistory.toggleAll )
	
	UpdateTodo<Todo>
		PUT "/todos/update" 
		payload id description  
		on success (Todo.update)
	
	DeleteTodo<TodoId> 
		DELETE "/todos/delete" 
		queryParams id 
		on success (Todo.delete)
	
	ClearDone<ClearDone> 
		DELETE "/todos/clear-done" 
		on success (Todo.clearDone) 
		
views 
	Todo {
		create(Todo)
		toggle(TodoToggle)
		toggleAll(ToggleAll)
		delete(TodoId)
		update(Todo)
		clearDone(ClearDone)
	}
	TodoDoneHistory {
		toggle(TodoToggle)
		toggleAll(ToggleAll)
	}

models 
	persistent TodoDoneHistory {
		Unique PrimaryKey NotNull String id
		NotNull String description
		DateTime doneDateTime
	}
	
	persistent Todo {
		Unique PrimaryKey NotNull String id
		NotNull String description
		NotNull Boolean done
		DateTime createdDateTime
		DateTime updatedDateTime
	}
	
	TodoList {
		List Todo todoList
	}
	
	
	TodoId {
		NotNull String id
	}

	TodoToggle {
		NotNull String id
		NotNull Boolean done
		Todo todoToBeToggled
		DateTime updatedDateTime
	}

	ToggleAll {
		NotNull Boolean done
		List Todo todosToBeToggled
		DateTime updatedDateTime
	}
	
	ClearDone {}
	
scenarios 
	GetAllTodos
	GIVEN 
		CreateTodo.success 
			uuid "123"
			systemTime "10.10.2019 8:22:00" {
				id : "123" 
				description : "xyz"
				createdDateTime : "10.10.2019 8:22:00"
			}
		CreateTodo.success 
			uuid "456"
			systemTime "10.10.2019 9:01:00" {
				id : "456" 
				description : "abc"
				createdDateTime : "10.10.2019 9:01:00"
			}
	WHEN GetAllTodos 
	THEN 200 
		response {
			todoList : [
				{
					id : "123" 
					description : "xyz"
					createdDateTime : "10.10.2019 8:22:00"
				}
				{
					id : "456" 
					description : "abc"
					createdDateTime : "10.10.2019 9:01:00"
				}
			]
		}

	DeleteTodo
	GIVEN 
		CreateTodo.success 
			uuid "123" {
			id : "123" 
			description : "todo 1"
			createdDateTime : "10.10.2019 8:22:00"
		}
		CreateTodo.success 
			uuid "234" {
			id : "234" 
			description : "todo 2"
			createdDateTime : "10.10.2019 9:01:00"
		}
	WHEN DeleteTodo 
		systemTime "10.10.2019 9:04:00" {
			id : "123"
		}
	THEN 200
		GetAllTodos 
		systemTime "10.10.2019 9:05:00"
		response {
			todoList : [{
				id : "234" 
				description : "todo 2"
				createdDateTime : "10.10.2019 9:01:00"
			}]
		}
		
		
		
		
		
		

