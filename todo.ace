JAVA

com.anfelisa.todo

ACE {
	GET "/todos/all" GetAllTodos uses TodoList
	POST "/todos/create" payload { Todo } CreateTodo uses Todo {
		on success (Todo.create)
	} 
	PUT "/todos/toggle" queryParams { Todo.id } ToggleTodo uses TodoToggle {
		on success ( Todo.toggle TodoDoneHistory.toggle )
	}
	PUT "/todos/toggle-all" ToggleAll uses ToggleAll {
		on success ( Todo.toggleAll TodoDoneHistory.toggleAll )
	}
	PUT "/todos/update" payload { Todo } UpdateTodo uses Todo {
		on success (Todo.update)
	}
	DELETE "/todos/delete" queryParams { Todo.id } DeleteTodo uses TodoId {
		on success (Todo.delete)
	}
	DELETE "/todos/clear-done" ClearDone uses ClearDone {
		on success (Todo.clearDone)
	}
}

views {
	Todo {
		create(Todo)
		toggle(TodoToggle)
		toggleAll(ToggleAll)
		delete(TodoId)
		update(Todo)
		clearDone(ClearDone)
	}
	TodoDoneHistory {
		toggle(TodoToggle)
		toggleAll(ToggleAll)
	}
}

models {
	schema public
	
	persistent TodoDoneHistory {
		Unique PrimaryKey NotNull String id
		NotNull String description
		DateTime doneDateTime
	}
	
	persistent Todo {
		Unique PrimaryKey NotNull String id
		NotNull String description
		NotNull Boolean done
		DateTime createdDateTime
		DateTime updatedDateTime
	}
	
	TodoList {
		List Todo todoList
	}
	
	TodoId {
		NotNull String id
	}

	TodoToggle {
		NotNull String id
		NotNull Boolean done
		Todo todoToBeToggled
		DateTime updatedDateTime
	}

	ToggleAll {
		NotNull Boolean done
		List Todo todosToBeToggled
		DateTime updatedDateTime
	}
	
}

data {
	TodoList : ( TodoList )
	Todo : ( Todo )
	TodoId : ( TodoId )
	TodoToggle : ( TodoToggle )
	ToggleAll : ( ToggleAll )
	ClearDone : (  )
}

